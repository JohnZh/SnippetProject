if (rootProject.ext.isDev) {
    apply plugin: 'com.android.application'
} else {
    apply plugin: 'com.android.library'
}

android {
    compileSdkVersion rootProject.ext.sdk.compileSdk

    defaultConfig {
        if (rootProject.ext.isDev) {
            applicationId "com.john.webview"
        }
        minSdkVersion rootProject.ext.sdk.minSdk
        targetSdkVersion rootProject.ext.sdk.targetSdk
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    sourceSets {
        main {
            if (rootProject.ext.isDev) {
                manifest.srcFile 'src/main/dev/AndroidManifest.xml'
                java.srcDirs 'src/main/dev/java'
            } else {
                manifest.srcFile 'src/main/AndroidManifest.xml'
            }
        }
    }

    viewBinding {
        enabled = true
    }

    compileOptions {
        sourceCompatibility 1.8
        targetCompatibility 1.8
    }

}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation 'androidx.appcompat:appcompat:1.1.0'
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'androidx.test.ext:junit:1.1.1'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.2.0'

    implementation 'androidx.webkit:webkit:1.2.0'

    implementation rootProject.ext.dependencies
    rootProject.ext.localModules.each {
        implementation project(it)
    }
    rootProject.ext.localProcessor.each {
        annotationProcessor project(it)
    }
}
